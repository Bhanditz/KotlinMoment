buildscript {
    ext.kotlin_version = '1.0.0-rc-1036'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

group            = 'me.mattak'
version          = '0.0.3'
archivesBaseName = 'moment'

ext {
    archiveName  = 'moment'
    userId       = 'mattak'
    userName     = 'Takuma Maruyama'
    userEmail    = 'mattak.me@gmail.com'
    gitUrl       = 'https://github.com/mattak/KotlinMoment.git'
    siteUrl      = 'https://github.com/mattak/KotlinMoment'
    issueUrl     = 'https://github.com/mattak/KotlinMoment/issues'
    projectName  = 'KotlinMoment'
    projectDescription = 'SwiftMoment for kotlin'
    scmUrl       = 'scm:git@github.com:mattak/KotlinMoment.git'
    licenseName  = 'The Apache License, Version 2.0'
    licenseUrl   = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    isReleaseVersion = !version.endsWith('SNAPSHOT')
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "junit:junit:4.12"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

jacoco {
    toolVersion = '0.7.5.201505241946'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

// task javadoc(type: Javadoc) {
//     source = sourceSets.main.java.srcDirs
//     classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
// }

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConvensions << {
    println project.getConvention()
}

if (gradle.startParameter.taskNames.contains('uploadArchives')) {
    apply from: 'publish_maven.gradle'
}

check.dependsOn jacocoTestReport
